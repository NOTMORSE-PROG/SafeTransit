// Gradle init script to fix Maven Central 403 rate limiting on GitHub Actions
// This script intercepts all repository definitions and redirects Maven Central to Google's mirror

def replaceMavenCentral(repository) {
    if (repository instanceof MavenArtifactRepository) {
        def url = repository.url.toString()
        if (url.contains("repo.maven.apache.org") || url.contains("repo1.maven.org")) {
            repository.url = uri('https://maven-central.storage-download.googleapis.com/maven2/')
            println "Redirected Maven Central to Google Mirror: ${repository.url}"
        }
    }
}

gradle.allprojects { project ->
    project.buildscript.repositories.all { repo ->
        replaceMavenCentral(repo)
    }
    project.repositories.all { repo ->
        replaceMavenCentral(repo)
    }
}

gradle.settingsEvaluated { settings ->
    settings.pluginManagement.repositories.all { repo ->
        replaceMavenCentral(repo)
    }
    settings.dependencyResolutionManagement.repositories.all { repo ->
        replaceMavenCentral(repo)
    }
}
